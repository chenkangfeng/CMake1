#
# Usage:
#	cmd_move(<source> <destination>)
#	cmd_mv(<source> <destination>)
#
function(cmd_move source destination)
	cmd_absolute(source destination)
	if(EXISTS ${source})
		if((NOT EXISTS ${destination}) OR ((NOT IS_DIRECTORY ${source}) AND (NOT IS_DIRECTORY ${destination})))
			file(RENAME ${source} ${destination})
		elseif((NOT IS_DIRECTORY ${source}) AND (IS_DIRECTORY ${destination}))
			get_filename_component(source_name ${source} NAME)
			file(RENAME ${source} ${destination}/${source_name})
		elseif(IS_DIRECTORY ${destination})
			file(GLOB sub_sources ${source}/*)
			foreach(sub_source ${sub_sources})
				get_filename_component(sub_source_name ${sub_source} NAME)
				cmd_move(${sub_source} ${destination}/${sub_source_name})
			endforeach()
			cmd_delete(${source})
		endif()
	elseif(${source} MATCHES "[*|?]")
		file(GLOB_RECURSE sub_sources ${source})
		if(sub_sources)
			get_filename_component(source_path ${source} PATH)
			string(LENGTH ${source_path} source_path_length)
			foreach(sub_source ${sub_sources})
				string(SUBSTRING ${sub_source} ${source_path_length} -1 sub_source_substring)
				file(RENAME ${sub_source} ${destination}${sub_source_substring})
			endforeach()
		endif()
	endif()
endfunction()

function(cmd_mv source destination)
	cmd_move(${source} ${destination})
endfunction()
