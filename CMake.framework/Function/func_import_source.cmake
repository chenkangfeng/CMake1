#
# Usage:
#	func_import_source(<root_path> <source_files>
#					   [DIRECTORY <directory1> ...]
#					   [EXTENSION <extension1> ...]
#					   [RECURSION])
#
function(func_import_source root_path source_files)
	cmd_absolute(root_path)
	set(source_list)
	set(parse_params DIRECTORY EXTENSION RECURSION)
	cmd_parse(parse_params ${ARGN})
	if(EXTENSION-FOUND)
		set(ext_list ${EXTENSION})
	else()
		set(ext_list "*")
	endif()
	foreach(sub_directory ${DIRECTORY})
		if(RECURSION-FOUND)
			_internal_import_source_recursion(${root_path} ${sub_directory} sub_source_files ${ext_list})
		else()
			_internal_import_source(${root_path} ${sub_directory} sub_source_files ${ext_list})
		endif()
		list(APPEND source_list ${sub_source_files})
	endforeach()
	set(${source_files} ${source_list} PARENT_SCOPE)
endfunction()

function(_internal_import_source root_path sub_directory source_files)
	set(full_directory ${root_path}/${sub_directory})
	cmd_absolute(full_directory)
	set(source_list)
	if(IS_DIRECTORY ${full_directory})
		include_directories(${full_directory})
		foreach(ext ${ARGN})
			cmd_ls(${full_directory}/[^.]*${ext} sub_source_files FILE)
			if(${sub_directory} STREQUAL ".")
				source_group("" FILES ${sub_source_files})
			else()
				string(REPLACE "/" "\\" win_style_sub_directory ${sub_directory})
				source_group(${win_style_sub_directory} FILES ${sub_source_files})
			endif()
			list(APPEND source_list ${sub_source_files})
		endforeach()
	endif()
	set(${source_files} ${source_list} PARENT_SCOPE)
endfunction()

function(_internal_import_source_recursion root_path sub_directory source_files)
	set(full_directory ${root_path}/${sub_directory})
	cmd_absolute(full_directory)
	set(source_list)
	if(IS_DIRECTORY ${full_directory})
		_internal_import_source(${root_path} ${sub_directory} sub_source_files ${ARGN})
		list(APPEND source_list ${sub_source_files})
		cmd_ls(${full_directory} sub_source_folders FOLDER)
		foreach(sub_source_folder ${sub_source_folders})
			get_filename_component(sub_source_folder_name ${sub_source_folder} NAME)
			_internal_import_source_recursion(${root_path} ${sub_directory}/${sub_source_folder_name}
											  sub_source_files ${ARGN})
			list(APPEND source_list ${sub_source_files})
		endforeach()
	endif()
	set(${source_files} ${source_list} PARENT_SCOPE)
endfunction()
